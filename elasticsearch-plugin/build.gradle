buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.elasticsearch.gradle:build-tools:${project.esVersion}")
    }
}

def projectVersion = project.projectVersion?.trim() ? project.projectVersion : "dev";
def generatedVersion = "${projectVersion}-${project.esVersion}"

group 'pl.suchenia.esmonitor'
version generatedVersion

if (!project.hasProperty('skipEsPlugin')) {
  apply plugin: 'idea'
  apply plugin: 'elasticsearch.esplugin'

  jar {
      manifest {
          attributes 'Implementation-Title': 'ElasticSearch Prometheus plugin', 'Implementation-Version': project.version
      }
  }

  esplugin {
      description 'Elasticsearch plugin to export status data in prometheus.io format'
      classname 'pl.suchenia.elasticsearchPrometheusMetrics.PrometheusExporterPlugin'
      licenseFile file("../LICENSE.txt")
      noticeFile file("../NOTICE.txt")
      licenseHeaders.enabled = false
      version generatedVersion
  }

  ext.projectSubstitutions = [:]
}

// Temporary fix for 7.3.0 https://github.com/elastic/elasticsearch/issues/45073
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute project(':rest-api-spec') with module ("org.elasticsearch:rest-api-spec:${project.esVersion}")
    }
}

repositories {
    mavenCentral()
    jcenter()
}
